addonName = "Improved Fire Logic & API"
resource.AddWorkshop( "2805142659" )
ENTITY = FindMetaTable( "Entity" )

flammableMaterials = list.GetForEdit( "IFL - Flammable Materials", false )
:WaterLevel, :IsOnFire, :GetMaterialType, :EntIndex = ENTITY
:Run, :Add = hook
CurTime = CurTime

-- Default unflammable materials
-- https://wiki.facepunch.com/gmod/Enums/MAT
flammableMaterials[ MAT_BLOODYFLESH ] = true
flammableMaterials[ MAT_ALIENFLESH ] = true
flammableMaterials[ MAT_ANTLION ] = true
flammableMaterials[ MAT_FOLIAGE ] = true
flammableMaterials[ MAT_PLASTIC ] = true
flammableMaterials[ MAT_GRASS ] = true
flammableMaterials[ MAT_FLESH ] = true
flammableMaterials[ MAT_WOOD ] = true

-- Strangely, but all sofa's are made of earth :||
flammableMaterials[ MAT_DIRT ] = true

IsFlammableModel = nil
do

    cache = {}

    IsFlammableModel = ( entity ) ->
        result = cache[ entity\GetModel! ]
        if result == nil
            result = false
            :ModelKeyValues = util.GetModelInfo( entity\GetModel! )
            if ModelKeyValues ~= nil
                :fire_interactions = util.KeyValuesToTable( ModelKeyValues )
                if fire_interactions ~= nil
                    result = fire_interactions.flammable == "yes"

            cache[ entity\GetModel! ] = result

        return result

    ENTITY.IsFlammableModel = IsFlammableModel

Add "AllowEntityIgnite", "Default Fire Logic", ( entity ) ->
    if WaterLevel( entity ) > 1
        return false

    if entity\IsNPC!
        return

    if entity\IsPlayer!
        if entity\Alive!
            return

        return false

    if IsFlammableModel( entity )
        return

    unless flammableMaterials[ GetMaterialType( entity ) ]
        return false

timer.Simple 0, ->

    -- Ignite
    sourceIgnite = ENTITY.SourceIgnite
    unless isfunction( sourceIgnite )
        sourceIgnite = ENTITY.Ignite
        ENTITY.SourceIgnite = sourceIgnite

    igniteEntity = nil
    do

        :Create, :Remove = timer

        igniteEntity = ( entity, length, radius ) ->
            timerName = "Source::IgniteEntity #" .. EntIndex( entity )
            Create timerName, length, 1, ->
                Remove timerName

                if entity\IsValid!
                    entity\Extinguish!

            sourceIgnite( entity, length, radius )

    ENTITY.Ignite = ( entity, length, radius ) ->
        if IsOnFire( entity ) or Run( "AllowEntityIgnite", entity ) == false
            return false

        unless length
            length = 1

        unless radius
            radius = 0

        entity.m_fIgniteStart = CurTime!
        entity.m_fIgniteLength = length
        entity.m_iIgniteRadius = radius

        igniteEntity( entity, length, radius )
        return true

    -- Extinguish
    sourceExtinguish = ENTITY.SourceExtinguish
    unless isfunction( sourceExtinguish )
        sourceExtinguish = ENTITY.Extinguish
        ENTITY.SourceExtinguish = sourceExtinguish

    ENTITY.Extinguish = ( entity, ... ) ->
        unless IsOnFire( entity )
            return false

        if Run( "AllowEntityExtinguish", entity ) == false
            if entity.m_fIgniteStart and ( CurTime! - entity.m_fIgniteStart ) < ( entity.m_fIgniteLength + 0.25 )
                entity.m_fIgniteStart = CurTime!
                igniteEntity( entity, entity.m_fIgniteLength or 1, entity.m_iIgniteRadius or 0 )

            return false

        entity.m_fLastExtinguish = CurTime!

        Run( "EntityExtinguish", entity )
        sourceExtinguish( entity, ... )
        return true

do

    DMG_BURN = DMG_BURN
    :band = bit

    Add "EntityTakeDamage", addonName, ( entity, damageInfo ) ->
        if not IsOnFire( entity ) or band( damageInfo\GetDamageType!, DMG_BURN ) == 0
            return

        return Run( "EntityBurns", entity, damageInfo )

-- Water extinguish
Add "EntityBurns", "Water Extinguish", ( entity, damageInfo ) ->
    if WaterLevel( entity ) > 1 and entity\Extinguish!
        return true

do

    :Length = FindMetaTable( "Vector" )
    :GetVelocity = ENTITY

    Add "EntityBurns", "Hight Speed Extinguish", ( entity, damageInfo ) ->
        if Length( GetVelocity( entity ) ) > 1500 and entity\Extinguish!
            return true

do

    ifl_water_extinguish_sound = CreateConVar( "ifl_water_extinguish_sound", "1", bit.bor( FCVAR_ARCHIVE, FCVAR_NOTIFY ), "If the value is set to 1, extinguishing entities in water will emit specific sound.", 0, 1 )
    CHAN_STATIC = CHAN_STATIC
    :random = math

    Add "OnEntityWaterLevelChanged", "Water Extinguish", ( entity, oldWaterLevel, newWaterLevel ) ->
        if newWaterLevel > 1 and oldWaterLevel < newWaterLevel and entity\Extinguish! and ifl_water_extinguish_sound\GetBool!
            entity\EmitSound( "player/flame_out.ogg", random( 50, 75 ), random( 60, 180 ), 1, CHAN_STATIC )
